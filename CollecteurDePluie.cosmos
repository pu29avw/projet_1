Auteur: pu29avw
Date: 08.10.2024
Entreprise: DGEP
Description: CollecteurDePluie

Voici les ordres du programme CollecteurDePluie  :
	
	Effacer l'écran.
	Masquer le curseur.

	Afficher "+-------------------+\n".
	Afficher "|Collecteur de Pluie|\n".
	Afficher "+-------------------+".
	
	Afficher "\n\n*MENU*\n\n".
	
	Choisir la couleur rouge foncé pour le fond. 
	Afficher "Jouer".
	Afficher "\n".
	Choisir la couleur noir pour le fond.
	Afficher "Quitter".
	
	//Gestion du MENU
	Créer une zone mémoire nommée #lignePremiereOption avec la valeur 6.
	Créer une zone mémoire nommée #ligneDerniereOption avec la valeur 7.
	Créer une zone mémoire nommée #ligneOptionCourante avec #lignePremiereOption.
	Créer une zone mémoire nommée #touchePressee avec le texte "vide" .
	
	Répéter tant que #touchePressee est différente de "Enter"
		
		Attendre la prochaine touche et la stocker dans #touchePressee.
		
		//L'option jouer est choisie et qu'on est pas déja dessus
		Si #touchePressee est égale à "UpArrow" et #ligneOptionCourante est différente de #lignePremiereOption alors
			
			Placer le curseur à la ligne #lignePremiereOption.
			Placer le curseur à la colonne 0.
			Choisir la couleur rouge foncé pour le fond.
			Afficher "Jouer".
			Afficher "\n".
			Choisir la couleur noir pour le fond.
			Afficher "Quitter".
			
			//On met à jour l'option ciurante choisie
			Copier le résultat de (#ligneOptionCourante - 1) dans #ligneOptionCourante.
			
		//L'option quitter est choisie et on est pas déja dessus
		sinon si #touchePressee est égale à "DownArrow" et #ligneOptionCourante est différente de #ligneDerniereOption alors
			
			Placer le curseur à la ligne #lignePremiereOption.
			Placer le curseur à la colonne 0.
			Afficher "Jouer".
			Afficher "\n".
			Choisir la couleur rouge foncé pour le fond.
			Afficher "Quitter".
			
			//On met à jour l'option courante choisie
			Copier le résultat de (#ligneOptionCourante + 1) dans #ligneOptionCourante.
		?
	>>

	//Remise à zéro des couleurs
	Choisir la couleur noir pour le fond.
	
	//Si on est là c'est que la touche Enter a été pressée
	//On va donc regarder l'option choisie
	
	//Option 'Jouer'
	Si #ligneOptionCourante est égale à #lignePremiereOption alors 
		Effacer l'écran.
		Afficher "Bienvenue dans le Jeu". 
		Attendre 950ms.
		Effacer l'écran.
	
		Créer une zone mémoire nommée #colonneLimiteGauche avec la valeur 0.
		Créer une zone mémoire nommée #colonneLimiteDroite avec la valeur 15.
		Créer une zone mémoire nommée #collecteurX avec la valeur 7. 
		Créer une zone mémoire nommée #collecteurY avec la valeur 11.
		//Placement initial
		Placer le curseur à la ligne #collecteurY.
		Placer le curseur à la colonne #collecteurX. 
		Afficher "U".
	
		Créer une zone mémoire nommée #ligneLimite avec la valeur 10.
		Créer une zone mémoire nommée #goutteX avec la valeur 0.
		Créer une zone mémoire nommée #goutteY avec #ligneLimite.
		
		Créer une zone mémoire nommée #score avec la valeur 0.
		Placer le curseur à la colonne 18.
		Placer le curseur à la ligne 3.
		Afficher "Score: #score".
		
		Répéter tant que #touchePressee est différente de "Escape"
			Si ##touche.disponible alors
				Copier ##touche dans #touchePressee.
			
				//Réagir à la touche (faire bouger le bac à pluie)
				Si #touchePressee est égale à "LeftArrow" et #collecteurX est différent de #colonneLimiteGauche alors 
					//On efface l'ancienne position
					Placer le curseur à la ligne #collecteurY.
					Placer le curseur à la colonne #collecteurX.
					Afficher " ".
					
					//Calcule la nouvelle position
					Copier le résultat de (#collecteurX - 1) dans #collecteurX.
					
					//On affiche la nouvelle position
					Placer le curseur à la ligne #collecteurY.
					Placer le curseur à la colonne #collecteurX. 
					Afficher "U".
					
				sinon si #touchePressee est égale à "RightArrow" et #collecteurX est plus petit que #colonneLimiteDroite alors 
					//On efface l'ancienne position
					Placer le curseur à la ligne #collecteurY.
					Placer le curseur à la colonne #collecteurX.
					Afficher " ".
					
					//Calcule la nouvelle position
					Copier le résultat de (#collecteurX + 1) dans #collecteurX.
					
					//On affiche la nouvelle position
					Placer le curseur à la ligne #collecteurY.
					Placer le curseur à la colonne #collecteurX.
					Afficher "U".
				?
			?
			//Dessine la goutte à la nouvelle position 
			Placer le curseur à la ligne #goutteY. 
			Placer le curseur à la colonne #goutteX. 
			Afficher "*".
			
			//Comparaison des positions de la goutte et du collecteur pour le score
			Si #goutteY est égal à (#collecteurY - 1) et #goutteX est égal à #collecteurX alors 
				Copier le résultat de (#score+5) dans #score.
				Placer le curseur à la colonne 18.
				Placer le curseur à la ligne 3.
				Afficher "Score: #score".
			?
		
			//Simuler la pluie
			
			//Efface l'ancienne position de la goutte
			Placer le curseur à la ligne #goutteY.
			Placer le curseur à la colonne #goutteX.
			Afficher " ".
			
			//La goute est arrivée en bas, on en regénère une
			Si #goutteY est égale à #ligneLimite alors
				Copier 0 dans #goutteY .
				Placer un nombre aléatoire compris entre 1 et 15 dans la zone mémoire nommée #goutteX.
			et sinon
				//On fait descendre la goutte
				Copier le résultat de (#goutteY + 1) dans #goutteY.
			?
			//Dessine la goutte à la nouvelle position
			Placer le curseur à la ligne #goutteY.
			Placer le curseur à la colonne #goutteX.
			Afficher "*".
			
			//Vitesse du jeu (20 fps ici)
			Attendre 80ms.
		>>
	//Option 'Quitter'
	et sinon
		Effacer l'écran.
		Afficher "\n\nAu revoir !".
	
	?
Fin de la transmission.
